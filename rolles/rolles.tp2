BACKUP ~rolles/backup~

// AUTHOR ~serdrick@rol.ru~
SUPPORT ~http://www.shsforums.net/topic/36655-rolles-v500/~

VERSION ~v5.0.0~

README ~rolles/readme/rolles-readme-%LANGUAGE%.txt~ ~rolles/readme/rolles-readme-english.txt~


ALWAYS

	/* Converts tra files to UTF-8 */
	ACTION_DEFINE_ARRAY s#_reload BEGIN setup END
	ACTION_DEFINE_ARRAY s#_noconvert BEGIN END
	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charset = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/tra~
			noconvert_array = s#_noconvert
			reload_array = s#_reload
	END

END

AUTO_TRA ~rolles/tra/%s~

LANGUAGE ~Russian~
         ~russian~
         ~rolles/tra/russian/setup.tra~

LANGUAGE ~English~
         ~english~
         ~rolles/tra/english/setup.tra~

LANGUAGE ~French (traduction : La Voix 2 la Sagesse)~
         ~french~
         ~rolles/tra/french/setup.tra~


/* ==================================== *
 *         Rolles Safyer (ToB)          *
 * ==================================== */

BEGIN @0000
DESIGNATED 0 LABEL ~Rolles_Safyer~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @0020

// Rolles
// ------
COPY ~rolles/sto/s#rolles.sto~	~override~

COPY ~rolles/roll/s#rolles.cre~	~override~
	SAY NAME1 @0001
 	SAY NAME2 @0002

EXTEND_TOP ~ar0706.bcs~	~rolles/roll/s#rolles.baf~

COMPILE ~rolles/dlg/rolles.d~


// Items
// -----
COPY ~rolles/eff~	~override~

COPY ~rolles/itm/s#bag01.itm~	~override~
COPY ~rolles/sto/s#bag01.sto~	~override~

COPY ~rolles/itm/s#book01.itm~	~override~
COPY ~rolles/itm/s#book02.itm~	~override~

INCLUDE ~rolles/lib/rolles_items.tpa~

// Copy BAMs or convert them to EE
// -------------------------------
ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
	OUTER_SPRINT file-exc ~s#sw1h02 s#sw1h04 ~ // Special case: Description bams don't need to be converted.
	ACTION_BASH_FOR ~rolles/bam/~ ~^.+\.bam$~ BEGIN
		ACTION_TO_LOWER %BASH_FOR_FILE%
		ACTION_TO_LOWER %BASH_FOR_RES%
		ACTION_IF (~%file-exc%~ STRING_CONTAINS_REGEXP ~%BASH_FOR_RES%~ = 1) BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
				LPF ~gw_convert_inventory_bam_to_ee~ END
			BUT_ONLY
		END ELSE BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
		END
	END
END ELSE BEGIN
	COPY ~rolles/bam~	~override~
END


/* ==================================== *
 *  Bag of Holding in Irenicus Dungeon  *
 * ==================================== */

BEGIN @0010
DESIGNATED 10 LABEL ~Rolles_Bag_of_Holding_in_Irenicus_Dungeon~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @0020

COPY ~rolles/sto/s#bag02.sto~	~override~
COPY ~rolles/itm/s#bag02.itm~	~override~

EXTEND_BOTTOM ~ar0602.bcs~ ~rolles/bag/bag.baf~
