BACKUP ~weidu_external/backup/rolles~

// AUTHOR ~serdrick@rol.ru~
SUPPORT ~http://www.shsforums.net/topic/36655-rolles~

VERSION ~v5.0.5~

README ~rolles/readme/rolles-readme-%LANGUAGE%.txt~ ~rolles/readme/rolles-readme-english.txt~


ALWAYS
    OUTER_SPRINT MOD_FOLDER "rolles"
    MKDIR "weidu_external\tra\%MOD_FOLDER%"

    ACTION_DEFINE_ARRAY arrayNoConvert BEGIN setup END
    ACTION_DEFINE_ARRAY arrayReload BEGIN rolles END

	LAF HANDLE_CHARSETS
		INT_VAR
            from_utf8 = 1 //the source lang files of all languages are UTF8
            infer_charset = 1 //set the correct encoding for classic games based on the detected game and associated encoding
            verbose = 1 //display more debugging info
        STR_VAR
            default_language = english
			tra_path = EVAL ~%MOD_FOLDER%\tra~ // path to translation files, do not add slash at the end of it
            out_path = EVAL ~weidu_external\lang\%MOD_FOLDER%~ // path to temporary work directory, do not add slash at the end of it
            noconvert_array = arrayNoConvert
            reload_array = arrayReload
    END


END

AUTO_TRA ~rolles\tra\%s~
//AUTO_TRA ~weidu_external\tra\%MOD_FOLDER%\%s~
LANGUAGE ~English~
         ~english~
         ~rolles/tra/english/setup.tra~
         ~rolles/tra/english/rolles.tra~

LANGUAGE ~Russian~
         ~russian~
         ~rolles/tra/russian/setup.tra~
         ~rolles/tra/english/rolles.tra~

LANGUAGE ~French (traduction: La Voix 2 la Sagesse)~
         ~french~
         ~rolles/tra/french/setup.tra~
         ~rolles/tra/english/rolles.tra~

LANGUAGE ~Italiano (traduzione di ilot)~
         ~italian~
         ~rolles/tra/italian/setup.tra~
          ~rolles/tra/english/rolles.tra~

LANGUAGE ~Polski (tlumaczenie: Aristo)~
         ~polish~
         ~rolles/tra/polish/setup.tra~
         ~rolles/tra/english/rolles.tra~


/* ==================================== *
 *         Rolles Safyer (ToB)          *
 * ==================================== */

BEGIN @11000
DESIGNATED 0 LABEL ~rolles_safyer~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @90000

// Rolles
// ------
COPY ~rolles/sto/s#rolles.sto~	~override~

COPY ~rolles/roll/s#rolles.cre~	~override~
	SAY NAME1 @0
 	SAY NAME2 @0

EXTEND_TOP ~ar0706.bcs~	~rolles/roll/s#rolles.baf~
EXTEND_TOP ~ar0706.bcs~	~rolles/roll/AR4000.baf~

COMPILE EVALUATE_BUFFER ~rolles/dlg/rolles.d~ USING ~rolles.tra~


// Items
// -----
COPY ~rolles/eff~	~override~

COPY ~rolles/itm/s#bag01.itm~	~override~
COPY ~rolles/sto/s#bag01.sto~	~override~

COPY ~rolles/itm/s#book01.itm~	~override~
COPY ~rolles/itm/s#book02.itm~	~override~

INCLUDE ~rolles/lib/rolles_items.tpa~

// Copy BAMs or convert them to EE
// -------------------------------
ACTION_IF GAME_IS ~bg2ee eet~ BEGIN
	OUTER_SPRINT file-exc ~s#sw1h02 s#sw1h04 ~ // Special case: Description bams don't need to be converted.
	ACTION_BASH_FOR ~rolles/bam/~ ~^.+\.bam$~ BEGIN
		ACTION_TO_LOWER %BASH_FOR_FILE%
		ACTION_TO_LOWER %BASH_FOR_RES%
		ACTION_IF (~%file-exc%~ STRING_CONTAINS_REGEXP ~%BASH_FOR_RES%~ = 1) BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
				LPF ~gw_convert_inventory_bam_to_ee~ END
			BUT_ONLY
		END ELSE BEGIN
			COPY ~%BASH_FOR_FILESPEC%~ ~override/%BASH_FOR_FILE%~
		END
	END
END ELSE BEGIN
	COPY ~rolles/bam~	~override~
END


/* ==================================== *
 *  Bag of Holding in Irenicus Dungeon  *
 * ==================================== */

BEGIN @12000
DESIGNATED 10 LABEL ~rolles_bag_of_holding_in_irenicus_dungeon~
REQUIRE_PREDICATE GAME_IS ~tob bgt bg2ee eet~ @90000

COPY ~rolles/sto/s#bag02.sto~	~override~
COPY ~rolles/itm/s#bag02.itm~	~override~

EXTEND_BOTTOM ~ar0602.bcs~ ~rolles/bag/bag.baf~
